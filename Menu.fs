module App.Menu
open App.Utils
open App.Types


open System
open System.Threading

type MenuState =
| Active
| Terminated


type State<'C> = {
    MenuState: MenuState
    X: int
    Y: int
    CurSorSelection: int
    CursorX: int
    Commands: ('C * string) array
    RedrawScreen: bool
}


let initialState x y commands = 
    {
        MenuState = Active
        X = x
        Y = y
        CurSorSelection = 0
        CursorX = x-2
        Commands = commands
        RedrawScreen = true
    }

let dibujarTituloMenu () =
    mostrarMensaje 12 1 ConsoleColor.Magenta "👾✨ ════════════════════════════ ═ ✨👾"
    mostrarMensaje 15 2 ConsoleColor.Green   "  A L I E N   A T T A C K  "
    mostrarMensaje 12 3 ConsoleColor.Magenta "👾✨ ════════════════════════════ ═ ✨👾"

let drawMenu state =
    Console.Clear()
    dibujarTituloMenu()
    state.Commands
    |> Array.iteri (fun i (_,legend) ->
        mostrarMensaje state.X (state.Y+i) ConsoleColor.Cyan legend
    )
//se dibuja el asterisco al lado del comando seleccionado, usando la posición del menú y el indice de la selección
    mostrarMensaje state.CursorX (state.Y+state.CurSorSelection) ConsoleColor.Yellow "*"
let redrawScreen state =
    if state.RedrawScreen then
        Console.Clear()
        state |> drawMenu
        {state with RedrawScreen = false}
    else
        state

let updateMenuKeyboard<'C> key state =
    let newState =
        match key with 
        | ConsoleKey.UpArrow -> {state with CurSorSelection = max 0 (state.CurSorSelection-1)}
        | ConsoleKey.DownArrow -> {state with CurSorSelection = min (state.Commands.Length-1) (state.CurSorSelection+1)}
        | ConsoleKey.Enter -> {state with MenuState = Terminated}
        | _ -> state

    if newState <> state then 
        {newState with RedrawScreen = true}
    else
        state
// Esta función se encarga de procesar el teclado, actualizando el estado del menú según la tecla presionada
let processKeyboard state =
    if Console.KeyAvailable then 
        let k = Console.ReadKey true
        state
        |> updateMenuKeyboard k.Key
    else
        state
let rec mainLoop state =
    let newState = 
        state
        |> processKeyboard
        |> redrawScreen
    if newState.MenuState = Active then
        Thread.Sleep 25
        mainLoop newState
    else
        state

// esta funcion  se ejecuta en bucle hasta que el usuario seleciona una opcion y presiona enter

let mostrar x y commands =
    let oldForeground = Console.ForegroundColor
    Console.CursorVisible <- false

    let state =
        initialState x y commands
        |> mainLoop

    Console.CursorVisible <- true
    Console.ForegroundColor <- oldForeground
    Console.Clear()
    fst state.Commands[state.CurSorSelection]

//Al hacer el menú genérico a través del tipo 'C, el módulo no sabe ni le importa qué comandos específicos existen, permite ser utilizado en cualquier menu